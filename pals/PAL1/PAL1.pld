Name     PAL1        ;
PartNo   00          ;
Date     10/09/2022  ;
Revision 01          ;
Designer David Banks ;
Company  NA          ;
Assembly None        ;
Location             ;
Device   g22v10      ;

/* *************** INPUT PINS *********************/

PIN 1    = CLKX4;
PIN 2    = MRDY;
PIN 3    = BA;
PIN 4    = BS;
PIN 5    = RnW;
PIN 6    = A4;
PIN 7    = A5;
PIN 8    = A8;
PIN 9    = !CSIOINT;
PIN 10   = 16KMODE;
PIN 11   = QA19;
PIN 13   = E;

PIN 14   = Q;     // Not currently used
//PIN 15
PIN 16   = !CSUART;
PIN 17   = !WRMMU1;
PIN 18   = !WRMMU0;
PIN 19   = !WR;
PIN 20   = !RD;
PIN 21   = A8X;
PIN 22   = EX;
PIN 23   = QX;


// In E-mode, drive E and Q to the CPU
//
// Note: E lags Q

FIELD count = [EX, QX];

$DEFINE s0 'b'00
$DEFINE s1 'b'01
$DEFINE s2 'b'11
$DEFINE s3 'b'10

SEQUENCE count {
  PRESENT s0
    NEXT s1;
  PRESENT s1
    NEXT s2;
  PRESENT s2
    NEXT s3;
  PRESENT s3
    IF MRDY NEXT s0;
    DEFAULT NEXT s3;
}


EX.AR = 'b'0;
EX.SP = 'b'0;
QX.AR = 'b'0;
QX.SP = 'b'0;

// Tristate A8X when BA is high, as buffers reversed
A8X = A8 $ (BS & !BA & RnW);
A8X.OE = !BA;

// A5 A4
//  0  0 UART
//  0  1 MMU0
//  1  0 MMU1
//  0  1  1 MMU0+1

WRMMU0 = E & CSIOINT & A4 & !RnW;

WRMMU1 = E & CSIOINT & A5 & !RnW;

// CSUART = E & CSIOINT & !A5 & !A4;
CSUART = ((RnW & (E # Q)) # (!RnW & (E & Q))) & CSIOINT & !A5 & !A4;

RD = E & RnW;

// TODO - 16KMODE could be qualified by ENMMU, otherwise relying on QA19 pulldown
// to be able to write to RAM when MMU disable

WR = E & !RnW & (!16KMODE # !QA19);
