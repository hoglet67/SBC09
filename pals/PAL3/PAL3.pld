Name     PAL3        ;
PartNo   00          ;
Date     10/09/2022  ;
Revision 01          ;
Designer David Banks ;
Company  NA          ;
Assembly None        ;
Location             ;
Device   g16v8       ;

/* *************** INPUT PINS *********************/

PIN 1    = A6;
PIN 2    = A7;
PIN 3    = A8;
PIN 4    = A9;
PIN 5    = A10;
PIN 6    = A11;
PIN 7    = A12;
PIN 8    = A13;
PIN 9    = A14;
PIN 11   = A15;

PIN 12   = QA13;        // Pin 12 is output-only in complex mode
PIN 13   = QA14;
PIN 14   = QA19;
PIN 15   = 16KMODE;
PIN 16   = !ENMMU;
PIN 17   = !CSIOEXT;
PIN 18   = !CSIOINT;
PIN 19   = !CSIO;       // Pin 19 is output-only in complex mode

// Intermediates

// Decode &FC00-&FEFF
CSIO = A15 & A14 & A13 & A12 & A11 & A10 & (!A9 # !A8);

// Decde &FE00-&FE3F
CSIOINT = A15 & A14 & A13 & A12 & A11 & A10 & A9 & !A8 & !A7 & !A6;

// Decode &FC00=&FDFF, &FE40-&FEFF
CSIOEXT = A15 & A14 & A13 & A12 & A11 & A10 & (!A9 # (A9 & !A8 & (A7 # A6)));

// In 16K Mode, A13->QA13; in 8K Mode QA19->QA13
QA13 = (16KMODE & A13)  # (!16KMODE & QA19);

// When MMU Disabled, pass through A14->QA14 else tristate
QA14    = A14;
QA14.OE = !ENMMU;
